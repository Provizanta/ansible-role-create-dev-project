# {{ ansible_managed }}

option(ALLOW_CLANG_TIDY "Allow clang-tidy to run on files during compilation" ON)

if(ALLOW_CLANG_TIDY AND CMAKE_VERSION VERSION_GREATER 3.6)
    set(CLANG_TIDY_BINARY "clang-tidy" CACHE STRING "Clang-tidy binary name or path to the desired binary")
    set(CLANG_TIDY_EXTRA_ARGS "-header-filter=.*" CACHE STRING "Clang-tidy extra arguments to be passed to the binary")

    option(CLANG_TIDY_FIX "Use clang-tidy to perform suggested fixes" OFF)

    find_program(CLANG_TIDY_EXECUTABLE
        NAMES ${CLANG_TIDY_BINARY}
    )

    if(CLANG_TIDY_EXECUTABLE)
        message(STATUS "Found clang-tidy: adding inline analysis during compilation")
        if(CLANG_TIDY_FIX)
            set(CMAKE_CXX_CLANG_TIDY "${CLANG_TIDY_EXECUTABLE}" "-fix" "${CLANG_TIDY_EXTRA_ARGS}")
        else()
            set(CMAKE_CXX_CLANG_TIDY "${CLANG_TIDY_EXECUTABLE}" "${CLANG_TIDY_EXTRA_ARGS}")
        endif()
    endif()
endif()

mark_as_advanced(
    CLANG_TIDY_EXECUTABLE
    CLANG_TIDY_BINARY
    CLANG_TIDY_EXTRA_ARGS
    CLANG_TIDY_FIX
)
